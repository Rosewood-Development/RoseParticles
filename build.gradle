plugins {
    id 'com.gradleup.shadow' version '9.2.2'
    id 'maven-publish'
    id 'java-library'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(21))
        disableAutoTargetJvm()
    }

    compileJava {
        options.compilerArgs += ['-parameters']
        options.fork = true
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()

        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.0.1'
    }
}

group = 'dev.rosewood'
version = providers.exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
}.standardOutput.asText.map { it.trim() }.orElse("unknown").get()

dependencies {
    implementation project(':Plugin')
    implementation project(':Arcane')
    implementation project(':NMS:Wrapper')
    implementation project(path: ':NMS:v1_21_R3', configuration: 'reobf')
    implementation project(path: ':NMS:v1_21_R4', configuration: 'reobf')
    implementation project(path: ':NMS:v1_21_R5', configuration: 'reobf')
    implementation project(path: ':NMS:v1_21_R6', configuration: 'reobf')
}

jar {
    manifest {
        attributes("paperweight-mappings-namespace": "spigot")
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            from components.shadow
            artifactId = 'roseparticles'
            pom {
                name = 'roseparticles'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username = project.mavenUser
                    password = project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

shadowJar {
    archiveClassifier.set(null)

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseparticles.lib.rosegarden')

    exclude 'com/zaxxer/hikari/**/*.class'
    exclude 'org/slf4j/**/*.class'
}

build.dependsOn shadowJar
generateMetadataFileForShadowPublication.dependsOn jar
