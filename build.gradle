plugins {
    id 'com.gradleup.shadow' version '8.3.5'
    id 'maven-publish'
    id 'java-library'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.gradleup.shadow'
    apply plugin: 'maven-publish'

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(21))
        disableAutoTargetJvm()
    }

    compileJava {
        options.compilerArgs += ['-parameters']
        options.fork = true
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()

        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://repo.rosewooddev.io/repository/public/' }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.0.1'
    }

    publishing {
        repositories {
            if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
                maven {
                    credentials {
                        username project.mavenUser
                        password project.mavenPassword
                    }

                    def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                    def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                }
            }
        }
    }
}

group = 'dev.rosewood'

dependencies {
    implementation project(':Plugin')
    implementation project(':Arcane')
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'roseparticles'
            pom {
                name = 'roseparticles'
            }
        }
    }
}

shadowJar {
    archiveClassifier.set(null)

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseparticles.lib.rosegarden')

    exclude 'com/zaxxer/hikari/**/*.class'
    exclude 'org/slf4j/**/*.class'
}

build.dependsOn shadowJar
